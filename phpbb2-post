#!/usr/bin/perl -w

# this script accepts input in the form of email and post it to a phpbb2 forum
# website. the post subject will be taken from email header Subject.

# usage: phpbb2-post [options] email1 email2 ...
#        phpbb2-post [options] - ...

# options can be put in config file or specified on the command line.

# if topic_id is not specified, post will be a new topic
# post. otherwise, it's a topic reply post.

# temporary workaround for LWP 6+ which checks certificate by default
BEGIN { $ENV{PERL_LWP_SSL_VERIFY_HOSTNAME} = 0 }

use strict;
use Cwd;
use File::Slurp;
use Log::Log4perl qw(:easy);
use Mail::Internet;
use WWW::Mechanize;
use App::Options (
    option => {
        profile => { type => 'string', required => 0, default => 'default', },
        username => { type => 'string', required => 1, },
        password => { type => 'string', required => 1, },
        base_url => { type => 'string', required => 1, },
        forum_id => { type => 'int', required => 0, },
        topic_id => { type => 'int', required => 0, },
        delay => { type => 'int', required => 0, default => 4, },
        log_level => { type => 'string', required => 0, default => 'DEBUG' },
        bbcode => { type => 'bool', required => 0, default => 0, },
        obfuscate_link => { type => 'bool', require => 0, default => 0, },
        http_proxy           => {type=>'string'},
        https_proxy          => {type=>'string'},
        https_proxy_username => {type=>'string'},
        https_proxy_password => {type=>'string'},
    },
);

if ( $App::options{log_level} eq 'FATAL' ) {
    Log::Log4perl->easy_init($FATAL);
}
elsif ( $App::options{log_level} eq 'ERROR' ) {
    Log::Log4perl->easy_init($ERROR);
}
elsif ( $App::options{log_level} eq 'WARN' ) {
    Log::Log4perl->easy_init($WARN);
}
elsif ( $App::options{log_level} eq 'INFO' ) {
    Log::Log4perl->easy_init($INFO);
}
else { Log::Log4perl->easy_init($DEBUG) }

# tadinya mau pakai plugin phpBB, tapi gagal login mulu. kayaknya
# gara2x gak specify 'button' di submit_form. ya udah, sementara pake
# Mech mentah aja.

# cek argumen
unless (@ARGV) {
    print <<_;
Usage: $0 [options] email1 email2 ...
       $0 [options] -
_
    exit 1;
}

LOGDIE "Please specify topic_id OR forum_id"
    unless $App::options{topic_id} || $App::options{forum_id};

for (qw/https_proxy https_proxy_username https_proxy_password/) {
    $ENV{uc $_} = $App::options{$_} if defined $App::options{$_};
}
my $mech = WWW::Mechanize->new(noproxy=>defined($App::options{https_proxy}));

if (defined $App::options{http_proxy}) {
    $mech->proxy(['http'], $App::options{http_proxy});
}

#eval to trap Mech exceptions
eval {

$mech->get("$App::options{base_url}/login.php");
$mech->submit_form(
    form_number => 1,
    fields      => {
        username => $App::options{username},
        password => $App::options{password},
    },
    button => 'login',
);

my $content = $mech->content;
if ($content !~ /log\s*out/i) {
    my $reason = "Mechanize doesn't return page with /logout/";
    if ($content =~ m!<span class="gen">(.+?)<br /><br />Click <a href="login\.php\?redirect=">!) {
        $reason = $1;
    }
    LOGDIE "Can't login: $reason";
}

#print $mech->content();

my $orig_dir = getcwd();

my $i = 0;
POST: for my $file (@ARGV) {
    sleep $App::options{delay} if $i++;

    # get new posting form
    $mech->get(
        $App::options{topic_id}
        ? "$App::options{base_url}/posting.php?mode=reply&t=$App::options{topic_id}"
        : "$App::options{base_url}/posting.php?mode=newtopic&f=$App::options{forum_id}"
    );

    my $file_content = $file eq '-' ? [<STDIN>] : read_file($file, array_ref => 1);
    my $obj = Mail::Internet->new($file_content);
    if ( !$obj ) {
        ERROR "Can't parse posting #$i, skipped";
        next POST;
    }
    my $subj = $obj->head->get("Subject") || "(no subject)";
    $subj =~ s/\n//g;
    my $mesg = ($obj->body ? join("", @{ $obj->body }) : "(no message)");
    $mesg =~ s/[\x80-\xff]/_/g; # XXX can't handle accented character right now

    # submit it
    my $num = $ENV{POST_NUM} // "#$i";
    INFO "Posting $num ...";

    if ($App::options{obfuscate_link}) {
        $mesg =~ s!\bhttp(s?)://!hxxp$1://!ig;
    }

    #DEBUG "posting = $posting";
    $mech->submit_form(
        form_number => 1,
        fields      => {
            subject         => $subj,
            message         => $mesg,
            disable_html    => ($App::options{html}   ? "off" : "on"),
            disable_bbcode  => ($App::options{bbcode} ? "off" : "on"),
            disable_smilies => "on",
        },
        button => 'post',
    );

    #print $mech->content;

    unless ( $mech->content =~ /successfully/i ) {
        ERROR "Failed posting $num!";
    }
}

}; #eval
my $eval_err = $@;

if ($eval_err) {
    if ($eval_err =~ /Mechanize/) {
        print STDERR "mech->content: ", $mech->content, "\n\n";
    }
    die $eval_err;
}
