# This chunk of stuff was generated by App::FatPacker. To find the original
# file's code, look for the end of this BEGIN block or the string 'FATPACK'
BEGIN {
my %fatpacked;

$fatpacked{"Devel/Platform/Info.pm"} = <<'DEVEL_PLATFORM_INFO';
  package Devel::Platform::Info;
  
  use strict;
  use warnings;
  
  use vars qw($VERSION);
  $VERSION = '0.09';
  
  #----------------------------------------------------------------------------
  
  my %map = (
      # Unix (and like) family OSes
      'freebsd'   => 'BSD',
      'openbsd'   => 'BSD',
      'netbsd'    => 'BSD',
      'irix'      => 'Irix',
      'linux'     => 'Linux',
      'aix'       => 'Linux',
      'bsdos'     => 'Linux',
      'dgux'      => 'Linux',
      'dynixptx'  => 'Linux',
      'hpux'      => 'Linux',
      'dec_osf'   => 'Linux',
      'svr4'      => 'Linux',
      'unicos'    => 'Linux',
      'unicosmk'  => 'Linux',
      'sunos'     => 'Linux',
      'ultrix'    => 'Linux',
      'sco_sv'    => 'SCO',
      'sco3'      => 'SCO',
      'sco'       => 'SCO',
      'solaris'   => 'Solaris',
  
      # Windows family OSes
      'dos'       => 'Win32',
      'os2'       => 'Win32',
      'mswin32'   => 'Win32',
      'netware'   => 'Win32',
      'cygwin'    => 'Win32',
  
      # Mac family OSes
      'macos'     => 'Mac',
      'rhapsody'  => 'Mac',
      'darwin'    => 'Mac',
  
      # Other OSes
      'vms'       => 'Linux',
      'vos'       => 'Linux',
      'os390'     => 'Linux',
      'vmesa'     => 'Linux',
      'riscos'    => 'Linux',
      'amigaos'   => 'Linux',
      'beos'      => 'Linux',
      'machten'   => 'Linux',
      'mpeix'     => 'Linux',
  
  );
  
  #----------------------------------------------------------------------------
  
  sub new {
      my ($class) = @_;
      my $self    = {};
      bless  $self, $class;
      return $self;
  }
  
  sub get_info {
      my $self  = shift;
      my $data;
  
      my $plugin = $map{ lc $^O } || 'Linux';
  
      my $driver = 'Devel::Platform::Info::' . $plugin;
      my $require = $driver;
      $require =~ s!::!/!g;
  
      eval {
          require "$require.pm";
          $self->{driver} = $driver->new();
          $data = $self->{driver}->get_info();
      };
  
      return  if($@);
      return $data;
  }
  
  1;
  
  __END__
  
  =head1 NAME
  
  Devel::Platform::Info - Unified framework for obtaining common platform metadata
  
  =head1 SYNOPSIS
  
    use Devel::Platform::Info;
    my $info = Devel::Platform::Info->new();
    my $data = $info->get_info();
  
  =head1 DESCRIPTION
  
  This module is a wrapper to the drivers which can determine platform metadata
  regarding the currently running operating system.
  
  The intention of this distribution is to provide key identifying components
  regarding the platform currently being used, for the CPAN Testers test
  reports. Currently the reports do not often contain enough information to help
  authors understand specific failures, where the platform may be a factor.
  
  However, it is hoped that this distribution will find more uses far beyond the
  usage for CPAN Testers.
  
  =head1 INTERFACE
  
  =head2 The Constructor
  
  =over
  
  =item * new
  
  Simply constructs the object.
  
  =back
  
  =head2 Methods
  
  =over 4
  
  =item * get_info
  
  Returns a hash reference to the platform metadata.
  
  Returns at least the following keys:
  
    source
    archname
    osname
    osvers
    oslabel
    is32bit
    is64bit
    osflag
  
  Note that the 'source' key returns the commands and output used to obtain the
  metadata for possible future use.
  
  Further keys may be available to provide additional information if applicable
  to the specific operating system.
  
  =back
  
  =head1 REFERENCES
  
  The following links were used to understand how to retrieve the metadata:
  
    * http://alma.ch/perl/perloses.htm
  
  =head1 BUGS, PATCHES & FIXES
  
  There are no known bugs at the time of this release. However, if you spot a
  bug or are experiencing difficulties, that is not explained within the POD
  documentation, please send bug reports and patches to the RT Queue (see below).
  
  RT Queue: http://rt.cpan.org/Public/Dist/Display.html?Name=Devel-Platform-Info
  
  =head1 AUTHORS
  
    Barbie (BARBIE) <barbie@cpan.org>
    Brian McCauley (NOBULL) <nobull67@gmail.com>
    Colin Newell http://colinnewell.wordpress.com/
    Jon 'JJ' Allen (JONALLEN) <jj@jonallen.info>
  
  =head1 COPYRIGHT & LICENSE
  
    Copyright (C) 2010-2011 Birmingham Perl Mongers
  
    This module is free software; you can redistribute it and/or
    modify it under the Artistic License 2.0.
  
  =cut
DEVEL_PLATFORM_INFO

$fatpacked{"Devel/Platform/Info/BSD.pm"} = <<'DEVEL_PLATFORM_INFO_BSD';
  package Devel::Platform::Info::BSD;
  
  use strict;
  use warnings;
  
  use vars qw($VERSION);
  $VERSION = '0.09';
  
  #----------------------------------------------------------------------------
  
  my %commands = (
      '_uname1'   => 'uname -a',
      '_uname2'   => 'uname -mrr',
      'kname'     => 'uname -s',
      'kvers'     => 'uname -r',
      'osname'    => 'uname -o',
      'archname'  => 'uname -m',
  );
  
  #----------------------------------------------------------------------------
  
  sub new {
      my ($class) = @_;
      my $self = {};
      bless $self, $class;
  
      return $self;
  }
  
  sub get_info {
      my $self  = shift;
  
      for my $cmd (keys %commands) {
          $self->{cmds}{$cmd} = `$commands{$cmd} 2>/dev/null`;
          $self->{cmds}{$cmd} =~ s/\s+$//s;
          $self->{info}{$cmd} = $self->{cmds}{$cmd}   if($cmd =~ /^[^_]/);
      }
  
      $self->{info}{osflag}   = $^O;
      $self->{info}{kvers}    = lc $self->{info}{kvers};
      $self->{info}{kernel}   = lc($self->{info}{kname}) . '-' . $self->{info}{kvers};
      $self->{info}{osname}   = $self->{info}{kname};
      $self->{info}{oslabel}  = $self->{info}{kname};
      $self->{info}{osvers}   = $self->{info}{kvers};
      $self->{info}{osvers}   =~ s/-release.*//;
      $self->{info}{is32bit}  = $self->{info}{archname} !~ /(64|alpha)/ ? 1 : 0;
      $self->{info}{is64bit}  = $self->{info}{archname} =~ /(64|alpha)/ ? 1 : 0;
  
      # NOTE: 'sparc64' (64bit) and 'sparc' (32bit) both look like they identify
      # themselves as archname = 'sparc'. If true, is there any other way to
      # easily distinguish the difference?
  
      $self->{info}{source}{$commands{$_}} = $self->{cmds}{$_}    for(keys %commands);
      return $self->{info};
  }
  
  #----------------------------------------------------------------------------
  
  1;
  
  __END__
  
  =head1 NAME
  
  Devel::Platform::Info::BSD - Retrieve BSD platform metadata
  
  =head1 SYNOPSIS
  
    use Devel::Platform::Info::BSD;
    my $info = Devel::Platform::Info::BSD->new();
    my $data = $info->get_info();
  
  =head1 DESCRIPTION
  
  This module is a driver to determine platform metadata regarding the BSD
  family of operating systems. It should be called indirectly via it's parent
  Devel::Platform::Info
  
  =head1 INTERFACE
  
  =head2 The Constructor
  
  =over
  
  =item * new
  
  Simply constructs the object.
  
  =back
  
  =head2 Methods
  
  =over 4
  
  =item * get_info
  
  Returns a hash reference to the BSD platform metadata.
  
  Returns the following keys:
  
    source
    archname
    osname
    osvers
    oslabel
    is32bit
    is64bit
    osflag
  
    kernel
    kname
    kvers
  
  =back
  
  =head1 REFERENCES
  
  The following links were used to understand how to retrieve the metadata:
  
    * http://nixcraft.com/all-about-freebsd-openbsd-netbsd/234-freebsd-how-find-out-kernel-version.html
    * http://www.netbsd.org/ports/
  
  Thanks to Chris 'BINGOS' Williams for the pointers to the appropriate links.
  
  =head1 BUGS, PATCHES & FIXES
  
  There are no known bugs at the time of this release. However, if you spot a
  bug or are experiencing difficulties, that is not explained within the POD
  documentation, please send bug reports and patches to the RT Queue (see below).
  
  RT Queue: http://rt.cpan.org/Public/Dist/Display.html?Name=Devel-Platform-Info
  
  =head1 AUTHORS
  
    Barbie (BARBIE) <barbie@cpan.org>
    Brian McCauley (NOBULL) <nobull67@gmail.com>
    Colin Newell http://colinnewell.wordpress.com/
    Jon 'JJ' Allen (JONALLEN) <jj@jonallen.info>
  
  =head1 COPYRIGHT & LICENSE
  
    Copyright (C) 2010-2011 Birmingham Perl Mongers
  
    This module is free software; you can redistribute it and/or
    modify it under the Artistic License 2.0.
  
  =cut
DEVEL_PLATFORM_INFO_BSD

$fatpacked{"Devel/Platform/Info/Irix.pm"} = <<'DEVEL_PLATFORM_INFO_IRIX';
  package Devel::Platform::Info::Irix;
  
  use strict;
  use warnings;
  
  use vars qw($VERSION);
  $VERSION = '0.09';
  
  #----------------------------------------------------------------------------
  
  my %commands = (
      '_issue1'   => 'cat /etc/issue',
      '_issue2'   => 'cat /etc/.issue',
      '_uname'    => 'uname -a',
      'kname'     => 'uname -s',
      'kvers'     => 'uname -r',
      'osname'    => 'uname -o',
      'archname'  => 'uname -m',
  
      '_irix1'   => 'uname -R',   # IRIX specific
  );
  
  #----------------------------------------------------------------------------
  
  sub new {
      my ($class) = @_;
      my $self = {};
      bless $self, $class;
  
      return $self;
  }
  
  sub get_info {
      my $self  = shift;
  
      for my $cmd (keys %commands) {
          $self->{cmds}{$cmd} = `$commands{$cmd} 2>/dev/null`;
          $self->{cmds}{$cmd} =~ s/\s+$//s;
          $self->{info}{$cmd} = $self->{cmds}{$cmd}   if($cmd =~ /^[^_]/);
      }
  
      $self->{info}{osflag}   = $^O;
      $self->{info}{kernel}   = lc($self->{info}{kname}) . '-' . $self->{info}{kvers};
      $self->{info}{osname}   = 'IRIX';
      $self->{info}{oslabel}  = 'IRIX';
      $self->{info}{osvers}   = $self->{info}{kvers};
      $self->{info}{is32bit}  = $self->{info}{kname} !~ /64/ ? 1 : 0;
      $self->{info}{is64bit}  = $self->{info}{kname} =~ /64/ ? 1 : 0;
  
      $self->{info}{source}{$commands{$_}} = $self->{cmds}{$_}    for(keys %commands);
      return $self->{info};
  }
  
  #----------------------------------------------------------------------------
  
  1;
  
  __END__
  
  =head1 NAME
  
  Devel::Platform::Info::Irix - Retrieve Irix platform metadata
  
  =head1 SYNOPSIS
  
    use Devel::Platform::Info::Irix;
    my $info = Devel::Platform::Info::Irix->new();
    my $data = $info->get_info();
  
  =head1 DESCRIPTION
  
  This module is a driver to determine platform metadata regarding the Irix
  operating system. It should be called indirectly via it's parent
  Devel::Platform::Info
  
  =head1 INTERFACE
  
  =head2 The Constructor
  
  =over
  
  =item * new
  
  Simply constructs the object.
  
  =back
  
  =head2 Methods
  
  =over 4
  
  =item * get_info
  
  Returns a hash reference to the Irix platform metadata.
  
  Returns the following keys:
  
    source
    archname
    osname
    osvers
    oslabel
    is32bit
    is64bit
    osflag
  
    kernel
    kname
    kvers
  
  =back
  
  =head1 REFERENCES
  
  The following links were used to understand how to retrieve the metadata:
  
    * http://www.faqs.org/faqs/sgi/faq/admin/section-2.html
  
  =head1 BUGS, PATCHES & FIXES
  
  There are no known bugs at the time of this release. However, if you spot a
  bug or are experiencing difficulties, that is not explained within the POD
  documentation, please send bug reports and patches to the RT Queue (see below).
  
  RT Queue: http://rt.cpan.org/Public/Dist/Display.html?Name=Devel-Platform-Info
  
  =head1 AUTHORS
  
    Barbie (BARBIE) <barbie@cpan.org>
    Brian McCauley (NOBULL) <nobull67@gmail.com>
    Colin Newell http://colinnewell.wordpress.com/
    Jon 'JJ' Allen (JONALLEN) <jj@jonallen.info>
  
  =head1 COPYRIGHT & LICENSE
  
    Copyright (C) 2010-2011 Birmingham Perl Mongers
  
    This module is free software; you can redistribute it and/or
    modify it under the Artistic License 2.0.
  
  =cut
DEVEL_PLATFORM_INFO_IRIX

$fatpacked{"Devel/Platform/Info/Linux.pm"} = <<'DEVEL_PLATFORM_INFO_LINUX';
  package Devel::Platform::Info::Linux;
  
  use strict;
  use warnings;
  
  use vars qw($VERSION);
  $VERSION = '0.09';
  
  #----------------------------------------------------------------------------
  
  my %commands = (
      '_issue1'   => 'cat /etc/issue',
      '_issue2'   => 'cat /etc/.issue',
      '_uname'    => 'uname -a',
      '_lsb'      => 'lsb_release -a',
      'kname'     => 'uname -s',
      'kvers'     => 'uname -r',
      'osname'    => 'uname -o',
      'archname'  => 'uname -m',
  );
  
  my %default = ();
  
  my %archlinux = (
      '0.1'       => 'Homer',
      '0.2'       => 'Vega',
      '0.3'       => 'Firefly',
      '0.4'       => 'Dragon',
      '0.5'       => 'Nova',
      '0.6'       => 'Widget',
      '0.7'       => 'Wombat',
      '0.8'       => 'Voodoo',
      '2007.05'   => 'Duke',
      '2007.08'   => "Don't Panic",
      '2008.06'   => 'Overlord',
      '2009.02'   => '2009.02',
      '2009.08'   => '2009.08',
      '2010.05'   => '2010.05',
  );
  
  my %debian = (
      '1.1'       => 'buzz',
      '1.2'       => 'rex',
      '1.3'       => 'bo',
      '2.0'       => 'hamm',
      '2.1'       => 'slink',
      '2.2'       => 'potato',
      '3.0'       => 'woody',
      '3.1'       => 'sarge',
      '4.0'       => 'etch',
      '5.0'       => 'lenny',
      '6.0'       => 'squeeze',
      '7.0'       => 'wheezy',
  );
  
  my %fedora = (
      '1'         => 'Yarrow',
      '2'         => 'Tettnang',
      '3'         => 'Heidelberg',
      '4'         => 'Stentz',
      '5'         => 'Bordeaux',
      '6'         => 'Zod',
      '7'         => 'Moonshine',
      '8'         => 'Werewolf',
      '9'         => 'Sulphur',
      '10'        => 'Cambridge',
      '11'        => 'Leonidas',
      '12'        => 'Constantine',
      '13'        => 'Goddard',
      '14'        => 'Laughlin',
  );
  
  my %mandriva = (
      '5.1'       => 'Venice',
      '5.2'       => 'Leeloo',
      '5.3'       => 'Festen',
      '6.0'       => 'Venus',
      '6.1'       => 'Helios',
      '7.0'       => 'Air',
      '7.1'       => 'Helium',
      '7.2'       => 'Odyssey (called Ulysses during beta)',
      '8.0'       => 'Traktopel',
      '8.1'       => 'Vitamin',
      '8.2'       => 'Bluebird',
      '9.0'       => 'Dolphin',
      '9.1'       => 'Bamboo',
      '9.2'       => 'FiveStar',
      '10.0'      => 'Community and Official',
      '10.1'      => 'Community',
      '10.1'      => 'Official',
      '10.2'      => 'Limited Edition 2005',
      '2006.0'    => 'Mandriva Linux 2006',
      '2007'      => 'Mandriva Linux 2007',
      '2007.1'    => 'Mandriva Linux 2007 Spring',
      '2008.0'    => 'Mandriva Linux 2008',
      '2008.1'    => 'Mandriva Linux 2008 Spring',
      '2009.0'    => 'Mandriva Linux 2009',
      '2009.1'    => 'Mandriva Linux 2009 Spring',
      '2010.0'    => 'Mandriva Linux 2010 (Adélie)',
      '2010.1'    => 'Mandriva Linux 2010 Spring',
      '2010.2'    => 'Mandriva Linux 2010.2',
  );
  
  my %ubuntu = (
      '4.10'      => 'Warty Warthog',
      '5.04'      => 'Hoary Hedgehog',
      '5.10'      => 'Breezy Badger',
      '6.06'      => 'Dapper Drake',
      '6.10'      => 'Edgy Eft',
      '7.04'      => 'Feisty Fawn',
      '7.10'      => 'Gutsy Gibbon',
      '8.04'      => 'Hardy Heron',
      '8.10'      => 'Intrepid Ibex',
      '9.04'      => 'Jaunty Jackalope',
      '9.10'      => 'Karmic Koala',
      '10.04'     => 'Lucid Lynx',
      '10.10'     => 'Maverick Meerkat',
      '11.04'     => 'Natty Narwhal',
  );
  
  my %scientific = (
      '3.0.1'     => 'Feynman',
      '3.0.2'     => 'Feynman',
      '3.0.3'     => 'Feynman',
      '3.0.4'     => 'Feynman',
      '3.0.5'     => 'Feynman',
      '3.0.6'     => 'Feynman',
      '3.0.7'     => 'Feynman',
      '3.0.8'     => 'Feynman',
      '3.0.9'     => 'Legacy',
      '4.0'       => 'Beryllium',
      '4.1'       => 'Beryllium',
      '4.2'       => 'Beryllium',
      '4.3'       => 'Beryllium',
      '4.4'       => 'Beryllium',
      '4.5'       => 'Beryllium',
      '4.6'       => 'Beryllium',
      '4.7'       => 'Beryllium',
      '4.8'       => 'Beryllium',
      '5.0'       => 'Boron',
      '5.1'       => 'Boron',
      '5.2'       => 'Boron',
      '5.3'       => 'Boron',
      '5.4'       => 'Boron',
      '5.5'       => 'Boron',
      '6.0'       => 'Carbon',
  );
  
  my %distributions = (
      'Adamantix'             => { codenames => \%default,                        files => [ qw( /etc/adamantix_version ) ] },
      'Annvix'                => { codenames => \%default,                        files => [ qw( /etc/annvix-release ) ] },
      'Arch Linux'            => { codenames => \%archlinux,                      files => [ qw( /etc/arch-release ) ] },
      'Arklinux'              => { codenames => \%default,                        files => [ qw( /etc/arklinux-release ) ] },
      'Aurox Linux'           => { codenames => \%default,                        files => [ qw( /etc/aurox-release ) ] },
      'BlackCat'              => { codenames => \%default,                        files => [ qw( /etc/blackcat-release ) ] },
      'Cobalt'                => { codenames => \%default,                        files => [ qw( /etc/cobalt-release ) ] },
      'Conectiva'             => { codenames => \%default,                        files => [ qw( /etc/conectiva-release ) ] },
      'Debian'                => { codenames => \%debian,     key => 'debian',    files => [ qw( /etc/debian_version /etc/debian_release ) ] },
      'Fedora Core'           => { codenames => \%fedora,     key => 'fedora',    files => [ qw( /etc/fedora-release ) ] },
      'Gentoo Linux'          => { codenames => \%default,    key => 'gentoo',    files => [ qw( /etc/gentoo-release ) ] },
      'Immunix'               => { codenames => \%default,                        files => [ qw( /etc/immunix-release ) ] },
      'Knoppix'               => { codenames => \%default,                        files => [ qw( /etc/knoppix_version ) ] },
      'Libranet'              => { codenames => \%default,                        files => [ qw( /etc/libranet_version ) ] },
      'Linux-From-Scratch'    => { codenames => \%default,                        files => [ qw( /etc/lfs-release ) ] },
      'Linux-PPC'             => { codenames => \%default,                        files => [ qw( /etc/linuxppc-release ) ] },
      'Mandrake'              => { codenames => \%mandriva,                       files => [ qw( /etc/mandrake-release ) ] },
      'Mandriva'              => { codenames => \%mandriva,                       files => [ qw( /etc/mandriva-release /etc/mandrake-release /etc/mandakelinux-release ) ] },
      'Mandrake Linux'        => { codenames => \%mandriva,                       files => [ qw( /etc/mandriva-release /etc/mandrake-release /etc/mandakelinux-release ) ] },
      'MkLinux'               => { codenames => \%default,                        files => [ qw( /etc/mklinux-release ) ] },
      'Novell Linux Desktop'  => { codenames => \%default,                        files => [ qw( /etc/nld-release ) ] },
      'Pardus'                => { codenames => \%default,    key => 'pardus',    files => [ qw( /etc/pardus-release ) ] },
      'PLD Linux'             => { codenames => \%default,                        files => [ qw( /etc/pld-release ) ] },
      'Red Flag'              => { codenames => \%default,    key => 'redflag',   files => [ qw( /etc/redflag-release ) ] },
      'Red Hat'               => { codenames => \%default,    key => 'redhat',    files => [ qw( /etc/redhat-release /etc/redhat_version ) ] },
      'Scientific Linux'      => { codenames => \%scientific,                     files => [ qw( /etc/lsb-release ) ] },
      'Slackware'             => { codenames => \%default,    key => 'slackware', files => [ qw( /etc/slackware-version /etc/slackware-release ) ] },
      'SME Server'            => { codenames => \%default,                        files => [ qw( /etc/e-smith-release ) ] },
      'Sun JDS'               => { codenames => \%default,                        files => [ qw( /etc/sun-release ) ] },
      'SUSE Linux'            => { codenames => \%default,    key => 'suse',      files => [ qw( /etc/SuSE-release /etc/novell-release ) ] },
      'SUSE Linux ES9'        => { codenames => \%default,    key => 'suse',      files => [ qw( /etc/sles-release ) ] },
      'Tiny Sofa'             => { codenames => \%default,                        files => [ qw( /etc/tinysofa-release ) ] },
      'Trustix Secure Linux'  => { codenames => \%default,                        files => [ qw( /etc/trustix-release ) ] },
      'TurboLinux'            => { codenames => \%default,                        files => [ qw( /etc/turbolinux-release ) ] },
      'Ubuntu Linux'          => { codenames => \%ubuntu,                         files => [ qw( /etc/lsb-release ) ] },
      'UltraPenguin'          => { codenames => \%default,                        files => [ qw( /etc/ultrapenguin-release ) ] },
      'UnitedLinux'           => { codenames => \%default,                        files => [ qw( /etc/UnitedLinux-release ) ] },
      'VA-Linux/RH-VALE'      => { codenames => \%default,                        files => [ qw( /etc/va-release ) ] },
      'Yellow Dog'            => { codenames => \%default,                        files => [ qw( /etc/yellowdog-release ) ] },
      'Yoper'                 => { codenames => \%default,                        files => [ qw( /etc/yoper-release ) ] },
  );
  
  my %version_pattern = (
      'gentoo'    => 'Gentoo Base System version (.*)',
      'debian'    => '(.+)',
      'suse'      => 'VERSION = (.*)',
      'fedora'    => 'Fedora Core release (\d+) \(',
      'redflag'   => 'Red Flag (?:Desktop|Linux) (?:release |\()(.*?)(?: \(.+)?\)',
      'redhat'    => 'Red Hat Linux release (.*) \(',
      'slackware' => '^Slackware (.+)$',
      'pardus'    => '^Pardus (.+)$',
  );
  
  my %oslabel_pattern = (
      'suse'      => '^(\S+)',
  );
  
  #----------------------------------------------------------------------------
  
  sub new {
      my ($class) = @_;
      my $self = {};
      bless $self, $class;
  
      return $self;
  }
  
  sub get_info {
      my $self  = shift;
  
      for my $cmd (keys %commands) {
          $self->{cmds}{$cmd} = `$commands{$cmd} 2>/dev/null`;
          $self->{cmds}{$cmd} =~ s/\s+$//s;
          $self->{info}{$cmd} = $self->{cmds}{$cmd}   if($cmd =~ /^[^_]/);
      }
  
      $self->{info}{osflag}       = $^O;
      $self->{info}{kernel}       = lc($self->{info}{kname}) . '-' . $self->{info}{kvers};
  
      $self->{info}{is32bit}      = $self->{info}{archname} !~ /_(64)$/ ? 1 : 0;
      $self->{info}{is64bit}      = $self->{info}{archname} =~ /_(64)$/ ? 1 : 0;
  
      if($self->{cmds}{'_lsb'}) {
          ($self->{info}{oslabel})    = $self->{cmds}{'_lsb'} =~ /Distributor ID:\s*(.*?)\n/si;
          ($self->{info}{osvers})     = $self->{cmds}{'_lsb'} =~ /Release:\s*(.*?)\n/si;
          ($self->{info}{codename})   = $self->{cmds}{'_lsb'} =~ /Codename:\s*(.*)\n?/si;
      } else {
          $self->_release_version();
      }
  
      $self->{info}{source}{$commands{$_}} = $self->{cmds}{$_}    for(keys %commands);
      return $self->{info};
  }
  
  #----------------------------------------------------------------------------
  
  sub _release_version {
      my $self = shift;
  
      for my $label (keys %distributions) {
          for my $file (@{ $distributions{$label}->{files} }) {
              next    unless(-f $file);
              my $line = `cat $file 2>/dev/null`;
  
              my ($version,$oslabel);
              if($distributions{$label}->{key}) {
                  if($version_pattern{ $distributions{$label}->{key} }) {
                      ($version) = $line =~ /$version_pattern{ $distributions{$label}->{key} }/si;
                  }
                  if($oslabel_pattern{ $distributions{$label}->{key} }) {
                      ($oslabel) = $line =~ /$oslabel_pattern{ $distributions{$label}->{key} }/si;
                  }
              }
  
              $version = $line    unless($version);
              $version =~ s/\s*$//;
  
              unless($oslabel) {
                  if($self->{cmds}{'_issue1'}) {
                      ($oslabel) = $self->{cmds}{'_issue1'} =~ /^(\S*)/;
                  } elsif($self->{cmds}{'_issue2'}) {
                      ($oslabel) = $self->{cmds}{'_issue2'} =~ /^(\S*)/;
                  }
                  $oslabel ||= $label;    # a last resort
              }
  
              $self->{info}{oslabel}  = $oslabel;
              $self->{info}{osvers}   = $version;
              $commands{'_cat'} = "cat $file";
              $self->{cmds}{'_cat'}  = $line;
  
              for my $vers (keys %{ $distributions{$label}->{codenames} }) {
                  if($version =~ /^$vers\b/) {
                      $self->{info}{codename} = $distributions{$label}->{codenames}{$vers};
                      return;
                  }
              }
  
              return;
          }
      }
  }
  
  1;
  
  __END__
  
  =head1 NAME
  
  Devel::Platform::Info::Linux - Retrieve Linux platform metadata
  
  =head1 SYNOPSIS
  
    use Devel::Platform::Info::Linux;
    my $info = Devel::Platform::Info::Linux->new();
    my $data = $info->get_info();
  
  =head1 DESCRIPTION
  
  This module is a driver to determine platform metadata regarding the Linux
  operating system. It should be called indirectly via it's parent 
  Devel::Platform::Info
  
  =head1 INTERFACE
  
  =head2 The Constructor
  
  =over
  
  =item * new
  
  Simply constructs the object.
  
  =back
  
  =head2 Methods
  
  =over 4
  
  =item * get_info
  
  Returns a hash reference to the Linux platform metadata.
  
  Returns the following keys:
  
    source
    archname
    osname
    osvers
    oslabel
    is32bit
    is64bit
    osflag
  
    codename
    kernel
    kname
    kvers
  
  =back
  
  =head1 REFERENCES
  
  The following links were used to understand how to retrieve the metadata:
  
    * http://distrowatch.com/
    * Wikipedia pages for various Linux and Unix based OSes
    * http://search.cpan.org/dist/Sys-Info-Driver-Linux
  
  =head1 BUGS, PATCHES & FIXES
  
  There are no known bugs at the time of this release. However, if you spot a
  bug or are experiencing difficulties, that is not explained within the POD
  documentation, please send bug reports and patches to the RT Queue (see below).
  
  RT Queue: http://rt.cpan.org/Public/Dist/Display.html?Name=Devel-Platform-Info
  
  =head1 AUTHORS
  
    Barbie (BARBIE) <barbie@cpan.org>
    Brian McCauley (NOBULL) <nobull67@gmail.com>
    Colin Newell http://colinnewell.wordpress.com/
    Jon 'JJ' Allen (JONALLEN) <jj@jonallen.info>
  
  =head1 COPYRIGHT & LICENSE
  
    Copyright (C) 2010-2011 Birmingham Perl Mongers
  
    This module is free software; you can redistribute it and/or
    modify it under the Artistic License 2.0.
  
  =cut
DEVEL_PLATFORM_INFO_LINUX

$fatpacked{"Devel/Platform/Info/Mac.pm"} = <<'DEVEL_PLATFORM_INFO_MAC';
  package Devel::Platform::Info::Mac;
  
  use strict;
  use warnings;
  
  use vars qw($VERSION);
  $VERSION = '0.09';
  
  #-------------------------------------------------------------------------------
  
  sub new {
      my ($class) = @_;
      my $self = {};
      bless $self, $class;
  
      return $self;
  }
  
  #-------------------------------------------------------------------------------
  
  sub get_info {
      my $self = shift;
      $self->{info}{osname} = 'Mac';
      $self->{info}{osflag} = $^O;
  
      my $uname_s = $self->_command('uname -s');
      if ($uname_s =~ /Darwin/i) {
          $self->{info}{oslabel} = 'OS X';
  
          my $productversion = $self->_command('sw_vers -productVersion');
          if ($productversion =~ /((\d+)\.(\d+)(\.(\d+))?)/) {
              my ($version, $major, $minor) = ($1, $2, $3);
              my $versions = _macos_versions();
              if (my $codename = $versions->{"$major.$minor"}) {
                  $self->{info}{codename} = $codename;
                  $self->{info}{osvers}  = $version;
              }
          }
      }
  
      if (my $arch = $self->_command('uname -p')) {
          chomp $arch;
          $self->{info}{archname} = $arch;
          $self->{info}{is32bit}  = $arch !~ /_(64)$/ ? 1 : 0;
          $self->{info}{is64bit}  = $arch =~ /_(64)$/ ? 1 : 0;
      }
  
      if (my $unamev = $self->_command('uname -v')) {
          chomp $unamev;
          $self->{info}{kernel} = $unamev;
      }
  
      $self->_command('uname -a');
  
      return $self->{info};
  }
  
  
  #-------------------------------------------------------------------------------
  
  sub _command {
      my $self    = shift;
      my $command = shift;
      my $result  = `$command`;
      chomp $result;
  
      $self->{info}{source}{$command} = $result;
  
      return $result;
  }
  
  #-------------------------------------------------------------------------------
  
  sub _macos_versions {
      return {
          '10.0' => 'Cheetah',
          '10.1' => 'Puma',
          '10.2' => 'Jaguar',
          '10.3' => 'Panther',
          '10.4' => 'Tiger',
          '10.5' => 'Leopard',
          '10.6' => 'Snow Leopard',
          '10.7' => 'Lion',
      };
  }
  
  #-------------------------------------------------------------------------------
  
  1;
  
  __END__
  
  =head1 NAME
  
  Devel::Platform::Info::Mac - Retrieve Mac platform metadata
  
  =head1 SYNOPSIS
  
    use Devel::Platform::Info::Mac;
    my $info = Devel::Platform::Info::Mac->new();
    my $data = $info->get_info();
  
  =head1 DESCRIPTION
  
  This module is a driver to determine platform metadata regarding the Mac
  operating system. It should be called indirectly via it's parent
  Devel::Platform::Info
  
  =head1 INTERFACE
  
  =head2 The Constructor
  
  =over
  
  =item * new
  
  Simply constructs the object.
  
  =back
  
  =head2 Methods
  
  =over 4
  
  =item * get_info
  
  Returns a hash reference to the Mac platform metadata.
  
  Returns the following keys:
  
    source
    archname
    osname
    osvers
    oslabel
    codename
    kernel
    is32bit
    is64bit
  
  =back
  
  =head1 BUGS, PATCHES & FIXES
  
  There are no known bugs at the time of this release. However, if you spot a
  bug or are experiencing difficulties, that is not explained within the POD
  documentation, please send bug reports and patches to the RT Queue (see below).
  
  RT Queue: http://rt.cpan.org/Public/Dist/Display.html?Name=Devel-Platform-Info
  
  =head1 AUTHORS
  
    Barbie (BARBIE) <barbie@cpan.org>
    Brian McCauley (NOBULL) <nobull67@gmail.com>
    Colin Newell http://colinnewell.wordpress.com/
    Jon 'JJ' Allen (JONALLEN) <jj@jonallen.info>
  
  =head1 COPYRIGHT & LICENSE
  
    Copyright (C) 2010-2011 Birmingham Perl Mongers
  
    This module is free software; you can redistribute it and/or
    modify it under the Artistic License 2.0.
  
  =cut
DEVEL_PLATFORM_INFO_MAC

$fatpacked{"Devel/Platform/Info/SCO.pm"} = <<'DEVEL_PLATFORM_INFO_SCO';
  package Devel::Platform::Info::SCO;
  
  use strict;
  use warnings;
  
  use vars qw($VERSION);
  $VERSION = '0.09';
  
  #----------------------------------------------------------------------------
  
  my %commands = (
      '_uname'    => 'uname -a',
      '_lsb'      => 'uname -X',
      'kname'     => 'uname -s',
      'kvers'     => 'uname -r',
  #    'osname'    => 'uname -o',
  #    'osvers'    => 'uname -v',
      'archname'  => 'uname -m',
  );
  
  my %releases = (
      '3.2.0'     => { oslabel => 'SCO UNIX System V/386',    codename => '' },
      '3.2.1'     => { oslabel => 'Open Desktop 1.0',         codename => '' },
      '3.2v2.0'   => { oslabel => 'Open Desktop 1.1',         codename => '' },
      '3.2v4.0'   => { oslabel => 'SCO UNIX',                 codename => '' },
      '3.2v4.1'   => { oslabel => 'Open Desktop 2.0',         codename => 'Phoenix' },
      '3.2v4.2'   => { oslabel => 'Open Desktop/Server 3.0',  codename => 'Tbird' },
      '3.2v5.0'   => { oslabel => 'OpenServer 5.0',           codename => 'Everest' },
      '3.2v5.0.2' => { oslabel => 'OpenServer 5.0.2',         codename => '' },
      '3.2v5.0.4' => { oslabel => 'OpenServer 5.0.4',         codename => 'Comet' },
      '3.2v5.0.5' => { oslabel => 'OpenServer 5.0.5',         codename => 'Davenport' },
      '3.2v5.0.6' => { oslabel => 'OpenServer 5.0.6',         codename => 'Freedom' },
      '3.2v5.0.7' => { oslabel => 'OpenServer 5.0.7',         codename => 'Harvey West' },
      '5'         => { oslabel => 'OpenServer 6.0',           codename => 'Legend' },
  );
  
  #----------------------------------------------------------------------------
  
  sub new {
      my ($class) = @_;
      my $self = {};
      bless $self, $class;
  
      return $self;
  }
  
  sub get_info {
      my $self  = shift;
  
      for my $cmd (keys %commands) {
          $self->{cmds}{$cmd} = `$commands{$cmd} 2>/dev/null`;
          $self->{cmds}{$cmd} =~ s/\s+$//s;
          $self->{info}{$cmd} = $self->{cmds}{$cmd}   if($cmd =~ /^[^_]/);
      }
  
      $self->{info}{osflag}       = $^O;
      $self->{info}{osname}       = 'SCO';
      $self->{info}{kernel}       = lc($self->{info}{kname}) . '-' . $self->{info}{kvers};
      ($self->{info}{osvers})     = $self->{cmds}{'_lsb'} =~ /Release\s*=\s*(.*?)\n/s;
      ($self->{info}{oslabel})    = $releases{ $self->{info}{osvers} }->{oslabel};
      ($self->{info}{codename})   = $releases{ $self->{info}{osvers} }->{codename};
  
      $self->{info}{is32bit}      = $self->{info}{archname} !~ /_(64)$/ ? 1 : 0;
      $self->{info}{is64bit}      = $self->{info}{archname} =~ /_(64)$/ ? 1 : 0;
  
      $self->{info}{source}{$commands{$_}} = $self->{cmds}{$_}    for(keys %commands);
      return $self->{info};
  }
  
  1;
  
  __END__
  
  =head1 NAME
  
  Devel::Platform::Info::SCO - Retrieve SCO Unix platform metadata
  
  =head1 SYNOPSIS
  
    use Devel::Platform::Info::SCO;
    my $info = Devel::Platform::Info::SCO->new();
    my $data = $info->get_info();
  
  =head1 DESCRIPTION
  
  This module is a driver to determine platform metadata regarding the SCO
  operating system. It should be called indirectly via it's parent
  Devel::Platform::Info
  
  =head1 INTERFACE
  
  =head2 The Constructor
  
  =over
  
  =item * new
  
  Simply constructs the object.
  
  =back
  
  =head2 Methods
  
  =over 4
  
  =item * get_info
  
  Returns a hash reference to the SCO platform metadata.
  
  Returns the following keys:
  
    source
    archname
    osname
    osvers
    oslabel
    is32bit
    is64bit
    osflag
  
    codename
    kernel
    kname
    kvers
  
  =back
  
  =head1 BUGS, PATCHES & FIXES
  
  There are no known bugs at the time of this release. However, if you spot a
  bug or are experiencing difficulties, that is not explained within the POD
  documentation, please send bug reports and patches to the RT Queue (see below).
  
  RT Queue: http://rt.cpan.org/Public/Dist/Display.html?Name=Devel-Platform-Info
  
  =head1 AUTHORS
  
    Barbie (BARBIE) <barbie@cpan.org>
    Brian McCauley (NOBULL) <nobull67@gmail.com>
    Colin Newell http://colinnewell.wordpress.com/
    Jon 'JJ' Allen (JONALLEN) <jj@jonallen.info>
  
  =head1 COPYRIGHT & LICENSE
  
    Copyright (C) 2010-2011 Birmingham Perl Mongers
  
    This module is free software; you can redistribute it and/or
    modify it under the Artistic License 2.0.
  
  =cut
DEVEL_PLATFORM_INFO_SCO

$fatpacked{"Devel/Platform/Info/Solaris.pm"} = <<'DEVEL_PLATFORM_INFO_SOLARIS';
  package Devel::Platform::Info::Solaris;
  
  use strict;
  use warnings;
  
  use vars qw($VERSION);
  $VERSION = '0.09';
  
  #----------------------------------------------------------------------------
  
  my %commands = (
      '_uname1'   => 'uname -a',
      '_showrev'  => 'showrev -a | grep -v "^Patch"',
      '_release'  => 'cat /etc/release',
      '_isainfo'  => '/usr/bin/isainfo -kv',
      'kname'     => 'uname -s',
      'kvers'     => 'uname -r',
      'archname'  => 'uname -m',
  );
  
  #----------------------------------------------------------------------------
  
  sub new {
      my ($class) = @_;
      my $self = {};
      bless $self, $class;
  
      return $self;
  }
  
  sub get_info {
      my $self  = shift;
  
      for my $cmd (keys %commands) {
          $self->{cmds}{$cmd} = `$commands{$cmd} 2>/dev/null`;
          $self->{cmds}{$cmd} =~ s/\s+$//s;
          $self->{info}{$cmd} = $self->{cmds}{$cmd}   if($cmd =~ /^[^_]/);
      }
  
      $self->{info}{osflag}   = $^O;
      $self->{info}{kernel}   = lc($self->{info}{kname}) . '-' . $self->{info}{kvers};
      $self->{info}{is32bit}  = $self->{cmds}{_isainfo} !~ /64-bit/s ? 1 : 0;
      $self->{info}{is64bit}  = $self->{cmds}{_isainfo} =~ /64-bit/s ? 1 : 0;
  
      ($self->{info}{osname}) = $self->{cmds}{_release} =~ /^(\S+)/;
      $self->{info}{oslabel}  = $self->{info}{osname};
      $self->{info}{osvers} = $self->{info}{kname};
      $self->{info}{osvers} =~ s/^5/2/;   # a bit of a hack :(
  
      # Question: Anyone know how to get the real version number for OpenSolaris?
      # i.e. "2008.05" or "2009.06"
  
      $self->{info}{source}{$commands{$_}} = $self->{cmds}{$_}    for(keys %commands);
      return $self->{info};
  }
  
  #----------------------------------------------------------------------------
  
  1;
  
  __END__
  
  =head1 NAME
  
  Devel::Platform::Info::Solaris - Retrieve Solaris platform metadata
  
  =head1 SYNOPSIS
  
    use Devel::Platform::Info::Solaris;
    my $info = Devel::Platform::Info::Solaris->new();
    my $data = $info->get_info();
  
  =head1 DESCRIPTION
  
  This module is a driver to determine platform metadata regarding the Solaris
  operating system. It should be called indirectly via it's parent
  Devel::Platform::Info
  
  =head1 INTERFACE
  
  =head2 The Constructor
  
  =over
  
  =item * new
  
  Simply constructs the object.
  
  =back
  
  =head2 Methods
  
  =over 4
  
  =item * get_info
  
  Returns a hash reference to the Solaris platform metadata.
  
  Returns the following keys:
  
    source
    archname
    osname
    osvers
    oslabel
    is32bit
    is64bit
    osflag
  
    kernel
    kname
    kvers
  
  =back
  
  =head1 REFERENCES
  
  The following links were used to understand how to retrieve the metadata:
  
    * http://www.symantec.com/connect/blogs/commands-find-out-solaris-os-version-ralus-rmal-issues
    * http://docs.sun.com/app/docs/doc/816-0211/6m6nc676p?a=view
    * http://docs.sun.com/app/docs/doc/816-5138/6mba6ua58?a=view
  
  =head1 BUGS, PATCHES & FIXES
  
  There are no known bugs at the time of this release. However, if you spot a
  bug or are experiencing difficulties, that is not explained within the POD
  documentation, please send bug reports and patches to the RT Queue (see below).
  
  RT Queue: http://rt.cpan.org/Public/Dist/Display.html?Name=Devel-Platform-Info
  
  =head1 AUTHORS
  
    Barbie (BARBIE) <barbie@cpan.org>
    Brian McCauley (NOBULL) <nobull67@gmail.com>
    Colin Newell http://colinnewell.wordpress.com/
    Jon 'JJ' Allen (JONALLEN) <jj@jonallen.info>
  
  =head1 COPYRIGHT & LICENSE
  
    Copyright (C) 2010-2011 Birmingham Perl Mongers
  
    This module is free software; you can redistribute it and/or
    modify it under the Artistic License 2.0.
  
  =cut
DEVEL_PLATFORM_INFO_SOLARIS

$fatpacked{"Devel/Platform/Info/Win32.pm"} = <<'DEVEL_PLATFORM_INFO_WIN32';
  package Devel::Platform::Info::Win32;
  
  use strict;
  use warnings;
  use POSIX;
  
  use vars qw($VERSION);
  $VERSION = '0.09';
  
  #----------------------------------------------------------------------------
  
  sub new {
      my ($class) = @_;
      my $self = {};
      bless $self, $class;
  
      return $self;
  }
  
  sub get_info {
      my $self  = shift;
  
      $self->{info}{osflag}       = $^O;
      my $inf = $self->_GetArchName();
      $self->{info}{oslabel} = $inf->{osLabel};
      $self->{info}{osvers} = $inf->{version};
      $self->{info}{archname} = $inf->{archname};
      $self->{info}{is32bit} = $self->{info}{archname} !~ /64/ ? 1 : 0;
      $self->{info}{is64bit} = $self->{info}{archname} =~ /64/ ? 1 : 0;
      $self->{info}{source} = $inf->{source};
      $self->{info}{wow64} = $inf->{wow64};
  
      return $self->{info};
  }
  
  sub _GetArchName
  {
      my $self = shift;
      my @uname = POSIX::uname();
      my @versions = Win32::GetOSVersion();
      my $info = $self->_InterpretWin32Info(@versions);
      $self->_AddPOSIXInfo($info, \@uname);
      return $info;
  }
  
  sub _AddPOSIXInfo
  {
      my $self = shift;
      my $info = shift;
      my $uname = shift;
      my $arch = $uname->[4];
      $info->{archname} = $arch;
      $info->{source} = {
          uname => $uname,
          GetOSVersion => $info->{source},
      };
      # used the tip from David Wang's blog,
      # http://blogs.msdn.com/b/david.wang/archive/2006/03/26/howto-detect-process-bitness.aspx
      if($ENV{'PROCESSOR_ARCHITEW6432'})
      {
          $info->{wow64} = 1;
      }
      else
      {
          $info->{wow64} = 0;
      }
  }
  
  sub _InterpretWin32Info
  {
      my $self = shift;
      my @versionInfo = @_;
      my ($string, $major, $minor, $build, $id, $spmajor, $spminor, $suitemask, $producttype, @extra)  = @versionInfo;
      my ($osname, $oslabel, $version, $source);
      my %info;
      my $NTWORKSTATION = 1;
      if($major == 5 && $minor == 2 && $producttype == $NTWORKSTATION)
      {
          $osname = 'Windows XP Pro 64';
      } elsif($major == 5 && $minor == 2 && $producttype != $NTWORKSTATION)
      {
          # server 2003, win home server
          # server 2003 R2
          # I need more info from GetSystemMetrics
          # be sure about the exact details.
          $osname = 'Windows Server 2003';
      } elsif($major == 5 && $minor == 1)
      {
          $osname = 'Windows XP';
      } elsif($major == 5 && $minor == 0)
      {
          $osname = 'Windows 2000';
      } elsif($major == 6 && $minor == 1 && $producttype == $NTWORKSTATION)
      {
          $osname = 'Windows 7';
      } elsif($major == 6 && $minor == 1 && $producttype != $NTWORKSTATION)
      {
          $osname = 'Windows Server 2008 R2';
      } elsif($major == 6 && $minor == 0 && $producttype == $NTWORKSTATION)
      {
          $osname = 'Windows Vista';
      } elsif($major == 6 && $minor == 0 && $producttype != $NTWORKSTATION)
      {
          $osname = 'Windows Server 2008';
      } elsif($major == 4 && $minor == 0 && $id == 1)
      {
          $osname = "Windows 95";
      } elsif($major == 4 && $minor == 10)
      {
          $osname = "Windows 98";
      } elsif($major == 4 && $minor == 90)
      {
          $osname = "Windows Me";
      } elsif($major == 4 && $minor == 0)
      {
          $osname = 'Windows NT 4';
      } elsif($major == 3 && $minor == 51)
      {
          $osname = "Windows NT 3.51";
      } else
      {
          $osname = 'Unrecognised - please file an RT case';
      }
      my $info =
      {
          osName => 'Windows',
          osLabel => $osname,
          version => "$major.$minor.$build.$id",
          source => \@versionInfo,
      };
      return $info;
  }
  
  
  1;
  
  __END__
  
  =head1 NAME
  
  Devel::Platform::Info::Win32 - Retrieve Windows platform metadata
  
  =head1 SYNOPSIS
  
    use Devel::Platform::Info::Win32;
    my $info = Devel::Platform::Info::Win32->new();
    my $data = $info->get_info();
  
  =head1 DESCRIPTION
  
  This module is a driver to determine platform metadata regarding the Win32
  operating system. It should be called indirectly via it's parent
  Devel::Platform::Info
  
  =head1 INTERFACE
  
  =head2 The Constructor
  
  =over
  
  =item * new
  
  Simply constructs the object.
  
  =back
  
  =head2 Methods
  
  =over 4
  
  =item * get_info
  
  Returns a hash reference to the Win32 platform metadata.
  
  Returns the following keys:
  
    source
    archname
    osname
    osvers
    oslabel
    is32bit
    is64bit
    osflag
    wow64
  
  On a 64 bit Windows if you are running 32 bit perl the archname is likely to
  indicate x86.  The wow64 variable will tell you if you are in fact running on
  x64 Windows.
  
  =back
  
  =head1 BUGS, PATCHES & FIXES
  
  The module cannot accurately tell the difference between the Windows Server
  2003 and Windows Server 2003 R2.
  
  The wow64 variable indicates whether or not you are running a 32 bit perl on a
  64 bit windows.  It uses the environment variable PROCESSOR_ARCHITEW6432 rather
  than the IsWow64Process call because it's simpler.
  
  If you spot a bug or are experiencing difficulties, that is not explained
  within the POD documentation, please send bug reports and patches to the RT
  Queue (see below).
  
  RT Queue: http://rt.cpan.org/Public/Dist/Display.html?Name=Devel-Platform-Info
  
  =head1 AUTHORS
  
    Barbie (BARBIE) <barbie@cpan.org>
    Brian McCauley (NOBULL) <nobull67@gmail.com>
    Colin Newell http://colinnewell.wordpress.com/
    Jon 'JJ' Allen (JONALLEN) <jj@jonallen.info>
  
  =head1 COPYRIGHT & LICENSE
  
    Copyright (C) 2010-2011 Birmingham Perl Mongers
  
    This module is free software; you can redistribute it and/or
    modify it under the Artistic License 2.0.
  
  =cut
DEVEL_PLATFORM_INFO_WIN32

$fatpacked{"SHARYANTO/DetectEnv.pm"} = <<'SHARYANTO_DETECTENV';
  package SHARYANTO::DetectEnv;
  
  our $env;
  
  if (-f "/etc/.backpacker-hpmini") {
      $env = "netbook-hpmini";
  } elsif (-f "/etc/.backpacker-eee") {
      $env = "netbook-eee";
  } elsif ("/etc/.builder") {
      $env = "pc";
  } else {
      die "Can't detect environment (e.g. /etc/.builder means builder)";
  }
  
  1;
  
SHARYANTO_DETECTENV

s/^  //mg for values %fatpacked;

unshift @INC, sub {
  if (my $fat = $fatpacked{$_[1]}) {
    open my $fh, '<', \$fat
      or die "FatPacker error loading $_[1] (could be a perl installation issue?)";
    return $fh;
  }
  return
};

} # END OF FATPACK CODE
#!/usr/bin/perl

use Data::Dump;
use Devel::Platform::Info;

dd(Devel::Platform::Info->new->get_info);
